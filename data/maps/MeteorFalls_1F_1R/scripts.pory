script MeteorFalls_PartySelect {
	lockall
	playse(SE_PIN)
	applymovement(LOCALID_METEOR_FALLS_RIVAL, moves(
		emote_exclamation_mark
		face_up
		))
	applymovement(LOCALID_PLAYER, moves(
		face_down
		))
	if (checkplayergender() == MALE){
		msgbox(format(
			"MAY: Good, you're here.\n"
			"I hear Prof. Cozmo up ahead.\p"
			"Do you have POKéMON ready in case we have to battle?"
			))
	} else {
		msgbox(format(
			"BRENDAN: Good, you're here.\n"
			"I hear Prof. Cozmo up ahead.\p"
			"Do you have POKéMON ready in case we have to battle?"
			))
	}
	special(SavePlayerParty)
	fadescreen(FADE_TO_BLACK)
	special(ChooseHalfPartyForBattle)
	waitstate
	goto_if_ne(VAR_RESULT, 0, MeteorFalls_MeteoriteScene)
	special(LoadPlayerParty)
	call(MeteorFalls_DeclineMulti)
}

script MeteorFalls_DeclineMulti {
	if (checkplayergender() == MALE){
		msgbox(format("MAY: Get ready, quick!"))
	} else {
		msgbox(format("BRENDAN: Get ready, quick!"))
	}
	closemessage
	applymovement(LOCALID_PLAYER, moves(
		walk_right
	))
	waitmovement(0)
	releaseall
}

script MeteorFalls_MeteoriteScene {
	addobject(LOCALID_METEOR_FALLS_AQUA_ADMIN)
	addobject(LOCALID_METEOR_FALLS_AQUA_GRUNT)
	applymovement(LOCALID_PLAYER, moves(
		player_run_left * 6
		player_run_left * 3
		player_run_down * 3
		walk_in_place_down
	))
	applymovement(LOCALID_METEOR_FALLS_RIVAL, moves(
		player_run_left * 6
		player_run_up
		player_run_left * 3
		player_run_down * 2
		player_run_left
		player_run_down
		walk_in_place_down
	))
	waitmovement(0)
	playbgm (MUS_ENCOUNTER_MAGMA, TRUE)
	msgbox(format("ADMIN MATT: Ha! We've got you Magma fools dead to rights.\p""When the leader gets here, he'll make short work of you."))
	closemessage
	playse(SE_PIN)
	applymovement(LOCALID_METEOR_FALLS_AQUA_ADMIN, moves(
		emote_exclamation_mark
		walk_in_place_fast_up
	))
	applymovement(LOCALID_METEOR_FALLS_MAGMA_ADMIN, moves(
		emote_exclamation_mark
		walk_in_place_fast_up
	))
	waitmovement(0)
	msgbox(format(
		"ADMIN MATT: So, Courtney…\n"
		"You got reinforcements…\p"
		"Gah! It doesn't matter!\n"
		"I'll take you all on!"))
	call(MeteorFalls_GetPartner)
}

script MeteorFalls_GetPartner {
	if (checkplayergender() == MALE){
		switch(var(VAR_STARTER_MON)){
		case 0: // Chose Chespin
			multi_2_vs_1(TRAINER_MATT, MeteorFalls_Text_AdminLose, PARTNER_MAY_CHESPIN)
			call(MeteorFalls_AdminDefeated)
		case 1: // Chose Tepig
			multi_2_vs_1(TRAINER_MATT, MeteorFalls_Text_AdminLose, PARTNER_MAY_TEPIG)
			call(MeteorFalls_AdminDefeated)
		case 2: // Chose Sobble
			multi_2_vs_1(TRAINER_MATT, MeteorFalls_Text_AdminLose, PARTNER_MAY_SOBBLE)
			call(MeteorFalls_AdminDefeated)
	}
	} else {
		switch(var(VAR_STARTER_MON)){
		case 0: // Chose Chespin
			multi_2_vs_1(TRAINER_MATT, MeteorFalls_Text_AdminLose, PARTNER_BRENDAN_CHESPIN)
			call(MeteorFalls_AdminDefeated)
		case 1: // Chose Tepig
			multi_2_vs_1(TRAINER_MATT, MeteorFalls_Text_AdminLose, PARTNER_BRENDAN_TEPIG)
			call(MeteorFalls_AdminDefeated)
		case 2: // Chose Sobble
			multi_2_vs_1(TRAINER_MATT, MeteorFalls_Text_AdminLose, PARTNER_BRENDAN_SOBBLE)
			call(MeteorFalls_AdminDefeated)
	}
	}
}

text MeteorFalls_Text_AdminLose {
	format("How'd I lose to some kids?")
}

script MeteorFalls_AdminDefeated {
	applymovement(LOCALID_PLAYER, moves(
		walk_in_place_up
	))
	applymovement(LOCALID_METEOR_FALLS_RIVAL, moves(
		walk_in_place_up
	))
	applymovement(LOCALID_METEOR_FALLS_AQUA_GRUNT, moves(
		walk_fast_up * 1
		walk_fast_right * 2
	))
	applymovement(LOCALID_METEOR_FALLS_MAGMA_ADMIN, moves(
		walk_fast_up * 2
		walk_fast_right * 2
		walk_fast_up * 2
		walk_fast_right
		walk_in_place_fast_down
	))
	applymovement(LOCALID_METEOR_FALLS_MAGMA_GRUNT, moves(
		walk_fast_up * 2
		walk_fast_right * 4
		walk_fast_up * 2
		walk_in_place_fast_down
	))
	waitmovement(0)
	msgbox(format(
		"ADMIN COURTNEY: Unanticipated factor… I don't know who you are, but you distracted that oaf for us so I kinda like you.\p"
		"Meteorite acquired…\n"
		"Mission complete…\l"
		"Mt. Chimney rendezvous commencing.\l"
		"Maybe, I'll be lucky enough to see you there…"
		))
	closemessage
	applymovement(LOCALID_METEOR_FALLS_RIVAL, moves(
		player_run_down
		player_run_left
		walk_in_place_left
	))
	applymovement(LOCALID_METEOR_FALLS_MAGMA_ADMIN, moves(
		walk_fast_right * 8
	))
	applymovement(LOCALID_METEOR_FALLS_MAGMA_GRUNT, moves(
		walk_fast_right * 8
	))
	waitmovement(0)
	removeobject(LOCALID_METEOR_FALLS_MAGMA_ADMIN)
	removeobject(LOCALID_METEOR_FALLS_MAGMA_GRUNT)
	applymovement(10, moves(face_right))
	if (checkplayergender() == MALE){
		msgbox(format("MAY: Prof. Cozmo, are you okay?"))
	} else {
		msgbox(format("BRENDAN: Prof. Cozmo, are you okay?"))
	}
	closemessage
	addobject(LOCALID_METEOR_FALLS_ARCHIE)
	applymovement(LOCALID_METEOR_FALLS_ARCHIE, moves(
		walk_right * 6
	))
	waitmovement(0)
	applymovement(LOCALID_METEOR_FALLS_ARCHIE, moves(
		walk_right
		walk_in_place_down
	))
	applymovement(LOCALID_METEOR_FALLS_AQUA_ADMIN, moves(
		walk_fast_left
		walk_fast_up
	))
	applymovement(LOCALID_METEOR_FALLS_AQUA_GRUNT, moves(
		walk_fast_up
		walk_fast_right * 2
	))
	waitmovement(0)
	msgbox(format(
		"ADMIN MATT: Boss! They got away…\p"
		"ARCHIE: Aye, we'll have to stop them atop Mt. Chimney."))
	applymovement(LOCALID_METEOR_FALLS_ARCHIE, moves(
		walk_right
		emote_exclamation_mark
		walk_in_place_down
	))
	playse(SE_PIN)
	waitse
	applymovement(LOCALID_PLAYER, moves(
		walk_in_place_up
	))
	msgbox(format(
		"ARCHIE: Haven't I seen you before?\p"
		"Ah, SLATEPORT's MUSEUM?\n"
		"So your name is {PLAYER}.\p"
		"You're one brave kid to put yourself in the middle of all this.\p"
		"TEAM MAGMA is a greedy bunch of fanatics who would destabalize the world for their misguided solutions."))
	closemessage
	applymovement(LOCALID_METEOR_FALLS_ARCHIE, moves(
		face_left
	))
	applymovement(LOCALID_METEOR_FALLS_AQUA_ADMIN, moves(
		walk_up
		face_right
	))
	waitmovement(0)
	msgbox(format(
		"ADMIN MATT: Sir, shouldn't we go after TEAM MAGMA?\p"
		"ARCHIE: Aye. Sorry, I got distracted.\n""I saw a bit of myself in that kid.\p""After them!"
	))
	closemessage
	applymovement(LOCALID_METEOR_FALLS_ARCHIE, moves(
		walk_fast_up * 2
		walk_fast_right * 11
	))
	applymovement(LOCALID_METEOR_FALLS_AQUA_ADMIN, moves(
		walk_fast_right * 1
		walk_fast_up * 2
		walk_fast_right * 10
	))
	applymovement(LOCALID_METEOR_FALLS_AQUA_GRUNT, moves(
		walk_fast_right * 2
		walk_fast_up * 2
		walk_fast_right * 9
	))
	waitmovement(0)
	savebgm(MUS_CAVE_OF_ORIGIN)
	fadedefaultbgm
	removeobject (LOCALID_METEOR_FALLS_ARCHIE)
	removeobject (LOCALID_METEOR_FALLS_AQUA_ADMIN)
	removeobject (LOCALID_METEOR_FALLS_AQUA_GRUNT)
	setflag (FLAG_HIDE_ROUTE_112_TEAM_MAGMA)
	setflag (FLAG_MET_ARCHIE_METEOR_FALLS)
	setflag (FLAG_HIDE_FALLARBOR_TOWN_BATTLE_TENT_SCOTT)
	setvar (VAR_METEOR_FALLS_STATE, 1)
	setobjectxyperm (LOCALID_METEOR_FALLS_RIVAL, 12, 22)
	releaseall
}

script MeteorFalls_Rival {
	lock
	faceplayer
	if (checkplayergender() == MALE){
		msgbox(format("MAY: I'll take care of Prof. Cozmo! You go to Mt. Chimney!"))
	} else {
		msgbox(format("BRENDAN: I'll take care of Prof. Cozmo! You go to Mt. Chimney!"))
	}
	release
}

script MeteorFalls_1F_1R_EventScript_SetRivalPos {
	setobjectxyperm (LOCALID_METEOR_FALLS_RIVAL, 12, 22)
}

raw `
MeteorFalls_1F_1R_MapScripts::
	map_script MAP_SCRIPT_ON_LOAD, MeteorFalls_1F_1R_OnLoad
	map_script MAP_SCRIPT_ON_TRANSITION, MeteorFalls_1F_1R_OnTransition
	.byte 0

MeteorFalls_1F_1R_OnLoad:
	call_if_set FLAG_SYS_GAME_CLEAR, MeteorFalls_1F_1R_EventScript_OpenStevensCave
	end

MeteorFalls_1F_1R_OnTransition:
	call_if_eq VAR_METEOR_FALLS_STATE, 1, MeteorFalls_1F_1R_EventScript_SetRivalPos
	end

MeteorFalls_1F_1R_EventScript_OpenStevensCave::
	setmetatile 4, 1, METATILE_MeteorFalls_CaveEntrance_Top, TRUE
	setmetatile 3, 2, METATILE_MeteorFalls_CaveEntrance_Left, TRUE
	setmetatile 4, 2, METATILE_MeteorFalls_CaveEntrance_Bottom, FALSE
	setmetatile 5, 2, METATILE_MeteorFalls_CaveEntrance_Right, TRUE
	return

MeteorFalls_1F_1R_EventScript_ProfCozmo::
	lock
	faceplayer
	goto_if_set FLAG_MET_PROF_COZMO, MeteorFalls_1F_1R_EventScript_MetCozmo
	setflag FLAG_MET_PROF_COZMO
	msgbox MeteorFalls_1F_1R_Text_MeetProfCozmo, MSGBOX_DEFAULT
	release
	end

MeteorFalls_1F_1R_EventScript_MetCozmo::
	msgbox MeteorFalls_1F_1R_Text_WhatsTeamMagmaDoingAtMtChimney, MSGBOX_DEFAULT
	release
	end

MeteorFalls_1F_1R_Text_MeetProfCozmo:
	.string "I… I'm COZMO…\n"
	.string "I'm a PROFESSOR…\p"
	.string "TEAM MAGMA asked me to guide them\n"
	.string "to METEOR FALLS…\p"
	.string "But they tricked me.\n"
	.string "They even took my METEORITE away…\p"
	.string "And then another similar bunch,\n"
	.string "TEAM AQUA showed up.\p"
	.string "After that…\n"
	.string "I don't understand any of it.\p"
	.string "But that TEAM MAGMA…\p"
	.string "What are they going to do with that\n"
	.string "METEORITE at MT. CHIMNEY?$"

MeteorFalls_1F_1R_Text_WhatsTeamMagmaDoingAtMtChimney:
	.string "PROF. COZMO: But that TEAM MAGMA…\p"
	.string "What are they going to do with that\n"
	.string "METEORITE at MT. CHIMNEY?$"
`
