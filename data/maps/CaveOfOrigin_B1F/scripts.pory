script CaveOfOrigin_ZinniaStory {
	lock
	msgbox(format(
		"ZINNIA: So, this is the CAVE OF ORIGIN.\p"
		"This is where the SUPER-ANCIENT POKéMON were born and from where their eggs, the RED and BLUE ORBS were taken…"
	))
	closemessage
	applymovement(LOCALID_CAVE_OF_ORIGIN_WALLACE, Common_Movement_FacePlayer)
	waitmovement(0)
	msgbox(format(
		"This story, however, does not end with mutual destruction…\p"
		"Eventually, the people realized the error of their ways and regretted what they had done,\l"
		"but when they tried to come to a truce,\l"
		"it was too late…\p"
		"KYOGRE and GROUDON had been driven to madness by their endless fighting and would not stop.\p"
		"In their desperation, these people, my ancestors, shared a wish to stop the war.\p"
		"Their wish shot across the sky like a comet and the DRAGON LORD, the planet's protector, descended from the heavens and quelled the fighting.\p"
		"The people were shaken by what they had done, but they would recover…\n"
		"Together, the two sides would inter the ORBS on MT. PYRE, which would serve as a memorial to the people and POKéMON who perished in the conflict."
	))
	msgbox(format(
		"ZINNIA: This mighty POKéMON, RAYQUAZA, stopped the fighting and restored peace to the world.\p"
		"After that, the ancient people built a sealed monument to the DRAGON LORD, SKY PILLAR.\p"
		"Atop this tower, RAYQUAZA could rest and watch over the world, aloof from petty requests."
		"{PLAYER}{KUN}, it is my ancestral duty to unseal the SKY PILLAR when things are at their most dire.\p"
		"There's not a moment to lose!\n"
		"Meet me at SKY PILLAR right away!"
	))
	closemessage
	playse (SE_EXIT)
	fadescreenspeed (FADE_TO_BLACK, 4)
	setflag (FLAG_WALLACE_GOES_TO_SKY_PILLAR)
	setvar (VAR_SOOTOPOLIS_CITY_STATE, 3)
	removeobject (LOCALID_CAVE_OF_ORIGIN_WALLACE)
	clearflag (FLAG_HIDE_SKY_PILLAR_WALLACE)
	fadescreen (FADE_FROM_BLACK)
	release
}

// Zinnia explains story:
// Kyogre and Groudon shaped the world in relative harmony.
// People came about and had a civil war.
// In this conflict, they grabbed the ORBS (eggs) of Kyogre and Groudon and used them to force the two to fight for them.
// The two fought so fiercely that they nearly destroyed the world.
// Rayquaza came down from the sky and stopped them, restoring peace.
// The ORBS were then interred on Mt. Pyre, a memorial to the people and POKéMON who perished in the conflict.

raw `
CaveOfOrigin_B1F_MapScripts::
	.byte 0

CaveOfOrigin_B1F_EventScript_Wallace::
	lock
	faceplayer
	msgbox CaveOfOrigin_B1F_Text_WallaceStory, MSGBOX_DEFAULT
	closemessage
	applymovement LOCALID_CAVE_OF_ORIGIN_WALLACE, Common_Movement_WalkInPlaceFasterUp
	waitmovement 0
	delay 60
	playse SE_PIN
	applymovement LOCALID_CAVE_OF_ORIGIN_WALLACE, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement LOCALID_CAVE_OF_ORIGIN_WALLACE, Common_Movement_Delay48
	waitmovement 0
	delay 30
	applymovement LOCALID_CAVE_OF_ORIGIN_WALLACE, Common_Movement_FacePlayer
	waitmovement 0
	message CaveOfOrigin_B1F_Text_WhereIsRayquaza
	waitmessage
	goto CaveOfOrigin_B1F_EventScript_WheresRayquaza

CaveOfOrigin_B1F_EventScript_WheresRayquaza::
	multichoice 0, 0, MULTI_WHERES_RAYQUAZA, FALSE
	switch VAR_RESULT
	case 0, CaveOfOrigin_B1F_EventScript_AtCaveOfOrigin
	case 1, CaveOfOrigin_B1F_EventScript_AtMtPyre
	case 2, CaveOfOrigin_B1F_EventScript_AtSkyPillar
	case 3, CaveOfOrigin_B1F_EventScript_DontRemember
	goto CaveOfOrigin_B1F_EventScript_DontRemember
	end

CaveOfOrigin_B1F_EventScript_AtCaveOfOrigin::
	message CaveOfOrigin_B1F_Text_ButWereInCaveOfOrigin
	waitmessage
	goto CaveOfOrigin_B1F_EventScript_WheresRayquaza

CaveOfOrigin_B1F_EventScript_AtMtPyre::
	message CaveOfOrigin_B1F_Text_OldLadyDidntMentionThat
	waitmessage
	goto CaveOfOrigin_B1F_EventScript_WheresRayquaza

CaveOfOrigin_B1F_EventScript_DontRemember::
	message CaveOfOrigin_B1F_Text_CantYouRememberSomehow
	waitmessage
	goto CaveOfOrigin_B1F_EventScript_WheresRayquaza

CaveOfOrigin_B1F_EventScript_AtSkyPillar::
	msgbox CaveOfOrigin_B1F_Text_WellHeadToSkyPillar, MSGBOX_DEFAULT
	closemessage
	playse SE_EXIT
	fadescreenspeed FADE_TO_BLACK, 4
	setflag FLAG_WALLACE_GOES_TO_SKY_PILLAR
	setvar VAR_SOOTOPOLIS_CITY_STATE, 3
	removeobject LOCALID_CAVE_OF_ORIGIN_WALLACE
	clearflag FLAG_HIDE_SKY_PILLAR_WALLACE
	fadescreen FADE_FROM_BLACK
	release
	end

CaveOfOrigin_B1F_Text_WallaceStory:
	.string "Ah, so you are {PLAYER}{KUN}?\n"
	.string "I've heard tales of your exploits.\p"
	.string "My name is WALLACE.\p"
	.string "I was once the GYM LEADER of\n"
	.string "SOOTOPOLIS, but something came up.\p"
	.string "So now, I've entrusted my mentor JUAN\n"
	.string "with the GYM's operation.\p"
	.string "… … … … … …\n"
	.string "… … … … … …\p"
	.string "GROUDON and KYOGRE, the two POKéMON\n"
	.string "wreaking havoc here, are considered\l"
	.string "to be super-ancient POKéMON.\p"
	.string "But there aren't just two super-\n"
	.string "ancient POKéMON.\p"
	.string "There is one more somewhere.\p"
	.string "Somewhere, there is a super-\n"
	.string "ancient POKéMON named RAYQUAZA.\p"
	.string "It's said that it was RAYQUAZA that\n"
	.string "becalmed the two combatants in\l"
	.string "the distant past.\p"
	.string "But even I have no clue as to\n"
	.string "RAYQUAZA's whereabouts…$"

CaveOfOrigin_B1F_Text_WhereIsRayquaza:
	.string "WALLACE: {PLAYER}{KUN}, do you perhaps\n"
	.string "know where RAYQUAZA is now?\p"
	.string "If you do, please tell me.$"

CaveOfOrigin_B1F_Text_ButWereInCaveOfOrigin:
	.string "WALLACE: The CAVE OF ORIGIN?\p"
	.string "But that's right here!\n"
	.string "I need you to do better than that!\p"
	.string "Please, I need you to think about\n"
	.string "where RAYQUAZA might be right now.$"

CaveOfOrigin_B1F_Text_OldLadyDidntMentionThat:
	.string "WALLACE: MT. PYRE?\p"
	.string "But when I met the old lady there\n"
	.string "earlier, she made no mention of it.\p"
	.string "I very much doubt that the old lady\n"
	.string "would try to hide something from me…\p"
	.string "{PLAYER}{KUN}, could you think about this\n"
	.string "more carefully for me?$"

CaveOfOrigin_B1F_Text_CantYouRememberSomehow:
	.string "WALLACE: Huh? You don't remember?\n"
	.string "Hmm… That's a problem…\p"
	.string "Can't you remember somehow?$"

CaveOfOrigin_B1F_Text_WellHeadToSkyPillar:
	.string "WALLACE: The SKY PILLAR?\p"
	.string "That's it!\n"
	.string "It must be the SKY PILLAR!\p"
	.string "{PLAYER}{KUN}, there's not a moment to lose!\n"
	.string "We'll head to the SKY PILLAR right away!$"

`
